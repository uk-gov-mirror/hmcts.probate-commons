plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'uk.gov.hmcts.java' version '0.12.67'
}

group 'com.github.hmcts'

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"
version buildNumber

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    mavenCentral()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

tasks.register('printVersion') {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/probate-commons"
    }
}

tasks.withType(Javadoc).configureEach {
    (options as StandardJavadocDocletOptions).addStringOption('Xdoclint:none', '-quiet')
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'probate-commons'
            version project.version
        }
    }
    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}

def versions = [
        lombok            : '1.18.42',
        jackson           : '2.19.0',
        apacheCommons     : '3.19.0',
        hibernateValidator: '9.0.1.Final',
        springfoxSwagger  : '3.0.0',
        springBoot        : '3.5.7',
        commonsIo         : '2.20.0',
        openfeign         : '3.8.0',
        pebble            : '3.2.4',
        itextpdf          : '9.1.0',
        itextpdf_html     : '6.2.1',
]

dependencies {
    implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: versions.openfeign
    implementation group: 'com.google.guava', name: 'guava', version: '33.5.0-jre'
    annotationProcessor group:'org.projectlombok', name:'lombok', version: versions.lombok

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: versions.apacheCommons
    implementation group: 'org.hibernate.validator', name: 'hibernate-validator', version: versions.hibernateValidator
    implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.11.0'

    implementation group: 'com.itextpdf', name: 'html2pdf', version: versions.itextpdf_html
    implementation group: 'com.itextpdf', name: 'itext-core', version: versions.itextpdf
    implementation group: 'io.pebbletemplates', name: 'pebble', version: versions.pebble

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
    testImplementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    testImplementation group: 'jakarta.el', name: 'jakarta.el-api', version: '6.0.1'
    testImplementation group: 'org.glassfish', name: 'jakarta.el', version: '5.0.0-M1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.20.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

test {
    maxHeapSize = "1024m"
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}

jar {
    enabled = true
}
